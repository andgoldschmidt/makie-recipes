{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using MakieCore\n",
    "import MakieCore: convert_arguments \n",
    "\n",
    "# This part would be the extension package (calls like lines, scatter, etc.)\n",
    "using CairoMakie\n",
    "using OrderedCollections\n",
    "\n",
    "# MakiePublication.jl has nice themes.\n",
    "\n",
    "import NamedTrajectories as NT"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Overview\n",
    "\n",
    "1. Define ability to plot trajectories with series\n",
    "2. Add a recipe so that we can send in custom kwargs (like transformation)\n",
    "3. Add a plot function similar to our previous that creates a figure\n",
    "4. Wrap the plot function in some way to allow for theme usage"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "traj = rand(NT.NamedTrajectory, 10);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "- We can use convert arguments to get access to plot and series commands\n",
    "- n.b. Series is not in MakieCore; I'm unsure how this will impact us.\n",
    "- We cannot use transformation as an argument because it conflicts with Makie's defaults."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function convert_arguments(\n",
    "    P::Type{<:Makie.Series}, \n",
    "    traj::NT.NamedTrajectory,\n",
    "    name::Symbol;\n",
    "    transform::Union{Function, Nothing}=nothing\n",
    ")\n",
    "    if !isnothing(transform)\n",
    "        transform_data = try\n",
    "            stack(transform.(eachcol(traj[name])))\n",
    "        catch\n",
    "            throw(ArgumentError(\"transform transformation of $(name) failed.\"))\n",
    "        end\n",
    "    else\n",
    "        transform_data = traj[name]\n",
    "    end\n",
    "\n",
    "    return convert_arguments(P, NT.get_times(traj), transform_data)\n",
    "end\n",
    "\n",
    "MakieCore.used_attributes(::Type{<:Makie.Series}, ::NT.NamedTrajectory, ::Symbol) = (:transform,)\n",
    "MakieCore.plottype(::NT.NamedTrajectory, ::Symbol) = Makie.Series"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "f = Figure()\n",
    "ax = Axis(f[1,1])\n",
    "\n",
    "labels = [\"x$i\" for i in 1:size(traj.x, 1)]\n",
    "\n",
    "p = plot!(\n",
    "    ax, traj, :x, \n",
    "    transform=x -> x .^ 2, \n",
    "    labels=labels, color=:seaborn_colorblind, marker=:circle\n",
    ")\n",
    "Legend(f[1,2], ax)\n",
    "f"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "- Name plot will have some default settings."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Add any desired series attributes here\n",
    "@recipe(NamedPlot, traj, input_name, output_name, transformation) do scene\n",
    "    Attributes(\n",
    "        # color=:glasbey_bw_minc_20_n256,\n",
    "        color=:seaborn_colorblind,\n",
    "        linestyle=theme(scene, :linestyle),\n",
    "        linewidth=theme(scene, :linewidth),\n",
    "        marker=:circle,\n",
    "        markersize=theme(scene, :markersize),\n",
    "        merge=false,\n",
    "    )\n",
    "end\n",
    "\n",
    "# Adds the ability to recall plot labels for a legend (extract series subplots)\n",
    "Makie.get_plots(P::NamedPlot) = Makie.get_plots(P.plots[1])\n",
    "\n",
    "# Plot existing component\n",
    "function Makie.plot!(\n",
    "    P::NamedPlot{<:Tuple{<:NT.NamedTrajectory, Symbol}};\n",
    "    kwargs...\n",
    ")\n",
    "    lift(P[:traj], P[:input_name]) do traj, name\n",
    "\n",
    "        if P[:merge][]\n",
    "            labels = fill(\"$name\", length(traj.components[name]))\n",
    "        else\n",
    "            labels = [\"$(name) $(i)\" for i in eachindex(traj.components[name])]\n",
    "        end\n",
    "\n",
    "        P = plot!(\n",
    "            P, traj, name;\n",
    "            labels=labels,\n",
    "            color = P[:color],\n",
    "            linestyle = P[:linestyle],\n",
    "            linewidth = P[:linewidth],\n",
    "            marker = P[:marker],\n",
    "            markersize = P[:markersize],\n",
    "        )\n",
    "        \n",
    "    end\n",
    "    return P\n",
    "end\n",
    "\n",
    "# Plot transformed component\n",
    "function Makie.plot!(\n",
    "    P::NamedPlot{<:Tuple{<:NT.NamedTrajectory, Symbol, Union{Nothing, Symbol, String}, Function}};\n",
    "    kwargs...\n",
    ")\n",
    "    lift(P[:traj], P[:input_name], P[:output_name], P[:transformation]) do traj, input, output, transform\n",
    "\n",
    "        if isnothing(output)\n",
    "            output = \"T($input)\"\n",
    "        end\n",
    "\n",
    "        if P[:merge][]\n",
    "            labels = fill(\"$output\", length(traj.components[input]))\n",
    "        else\n",
    "            labels = [\"$(output) $(i)\" for i in eachindex(traj.components[input])]\n",
    "        end\n",
    "\n",
    "        P = plot!(\n",
    "            P, traj, input;\n",
    "            transform=transform,\n",
    "            labels=labels,\n",
    "            color = P[:color],\n",
    "            linestyle = P[:linestyle],\n",
    "            linewidth = P[:linewidth],\n",
    "            marker = P[:marker],\n",
    "            markersize = P[:markersize],\n",
    "        )\n",
    "        \n",
    "    end\n",
    "    return P\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "f = Figure()\n",
    "ax = Axis(f[1,1])\n",
    "p = namedplot!(ax, traj, :x)\n",
    "Legend(f[1,2], ax)\n",
    "f"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Ability to apply transformations and merge names\n",
    "f = Figure()\n",
    "ax = Axis(f[1,1])\n",
    "p = namedplot!(ax, traj, :x, \"y\", x -> x .^ 2, linewidth=3, marker=:circle, merge=true)\n",
    "Legend(f[1,2], ax, merge=true)\n",
    "\n",
    "ax = Axis(f[2,1])\n",
    "p = namedplot!(ax, traj, :x, nothing, x -> x .^ 2, linewidth=3, marker=:circle)\n",
    "Legend(f[2,2], ax)\n",
    "f"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "- Plot trajectory will build the figure and allow use of themes"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Notes:\n",
    "- I've temporarily removed the vector of transformations (for each column) for simplicity. \n",
    "- What to do about ylims?\n",
    "- I also removed the latex parsing (that's probably a theme variable, one of the default themes is LaTeX). Need to review what visual features should move to theme, and what should stay here."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_trajectory(\n",
    "    traj::NT.NamedTrajectory,\n",
    "    names::Union{AbstractVector{Symbol}, Tuple{Vararg{Symbol}}}=traj.names;\n",
    "\n",
    "    # ---------------------------------------------------------------------------\n",
    "    # component specification keyword arguments\n",
    "    # ---------------------------------------------------------------------------\n",
    "\n",
    "    # whether or not to plot the timestep componenent\n",
    "    ignore_timestep::Bool=true,\n",
    "    \n",
    "    # whether or not to include unique labels for components\n",
    "    merge_labels::Union{Bool, AbstractVector{Bool}} = false,\n",
    "\n",
    "    # ---------------------------------------------------------------------------\n",
    "    # transformation keyword arguments\n",
    "    # ---------------------------------------------------------------------------\n",
    "\n",
    "    # transformations\n",
    "    transformations::OrderedDict{Symbol, <:Function} = OrderedDict{Symbol, Function}(),\n",
    "\n",
    "    # labels for transformed components\n",
    "    transformation_labels::AbstractVector{<:Union{Nothing, String}} = fill(nothing, length(transformations)),\n",
    "    \n",
    "    # whether or not to include unique labels for transformed components\n",
    "    merge_transformation_labels::Union{Bool, AbstractVector{Bool}} = false,\n",
    "    \n",
    "    # ---------------------------------------------------------------------------\n",
    "    # figure and axis keyword arguments\n",
    "    # ---------------------------------------------------------------------------\n",
    "    \n",
    "    fig_size::Tuple{Int, Int} = (1200, 800),\n",
    "    titlesize::Int=16,\n",
    "    xlabelsize::Int=16,\n",
    "\n",
    "    # ---------------------------------------------------------------------------\n",
    "    # plot keyword arguments (for all names)\n",
    "    # ---------------------------------------------------------------------------\n",
    "    kwargs...\n",
    ")\n",
    "\n",
    "    # parse arguments\n",
    "    if names isa Symbol\n",
    "        names = [names]\n",
    "    end\n",
    "\n",
    "    if merge_labels isa Bool\n",
    "        merge_labels = fill(merge_labels, length(names))\n",
    "    end\n",
    "\n",
    "    if merge_transformation_labels isa Bool\n",
    "        merge_transformation_labels = fill(merge_transformation_labels, length(transformations))\n",
    "    end\n",
    "\n",
    "    @assert length(merge_transformation_labels) == length(transformation_labels) == length(transformations)\n",
    "\n",
    "    # create figure\n",
    "    fig = Figure(size=fig_size)\n",
    "\n",
    "    # Default components\n",
    "    # ------------------\n",
    "    for (i, name) in enumerate(names)\n",
    "        ax = Axis(\n",
    "            fig[i, 1],\n",
    "            title = i == 1 ? \"Named Trajectory\" : \"\",\n",
    "            titlealign = :left,\n",
    "            titlesize = titlesize,\n",
    "            xticklabelsvisible = i == length(names),\n",
    "            xtickalign=1,\n",
    "            xlabel = i == length(names) ? \"time\" : \"\",\n",
    "            xlabelsize = xlabelsize,\n",
    "        )\n",
    "        merge = merge_labels[i]\n",
    "        namedplot!(ax, traj, name, merge=merge; kwargs...)\n",
    "        Legend(fig[i, 2], ax, merge=merge)\n",
    "    end\n",
    "\n",
    "    for i in 1:length(names) - 1\n",
    "        rowgap!(fig.layout, i, 0.0)\n",
    "    end\n",
    "\n",
    "\n",
    "    # Transformations\n",
    "    # ---------------\n",
    "    offset = length(names)\n",
    "\n",
    "    for (i, (input, transform)) in enumerate(transformations)\n",
    "        ax = Axis(\n",
    "            fig[offset + i, 1],\n",
    "            title = i == 1 ? \"Transformations\" : \"\",\n",
    "            titlealign = :left,\n",
    "            titlesize = titlesize,\n",
    "            xticklabelsvisible = i == length(transformations),\n",
    "            xtickalign = 1,\n",
    "            xlabel = i == length(transformations) ? \"time\" : \"\",\n",
    "            xlabelsize = xlabelsize,\n",
    "        )\n",
    "        output = transformation_labels[i]\n",
    "        merge = merge_transformation_labels[i]\n",
    "        namedplot!(ax, traj, input, output, transform, merge=merge; kwargs...)\n",
    "        Legend(fig[offset + i, 2], ax, merge=merge)\n",
    "    end\n",
    "\n",
    "    for i in 1:length(transformations)-1\n",
    "        rowgap!(fig.layout, offset + i, 0.0)\n",
    "    end\n",
    "\n",
    "    fig\n",
    "end\n",
    "\n",
    "function plot_trajectory(\n",
    "    theme::MakieCore.Theme,\n",
    "    args...;\n",
    "    kwargs...\n",
    ")\n",
    "    with_theme(theme) do\n",
    "        plot_trajectory(args...; kwargs...)\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_trajectory(\n",
    "    traj, \n",
    "    [:x, :u],\n",
    "    merge_labels=[true, false],\n",
    "    transformations=OrderedDict(:x => x -> [x[1] ^6],), \n",
    "    merge_transformation_labels=true,\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Test passing in series args"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_trajectory(\n",
    "    traj, \n",
    "    [:x, :u],\n",
    "    merge_labels=[true, false],\n",
    "    transformations=OrderedDict(:x => x -> [x[1] ^6],), \n",
    "    merge_transformation_labels=true,\n",
    "    linewidth=5,\n",
    "    color=:glasbey_bw_minc_20_n256,\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Test setting a theme."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_trajectory(theme_latexfonts(), traj, merge_labels=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.3",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
